import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.16'
    id 'org.jetbrains.kotlin.jvm' version '1.4-M1'
}

group 'com.github.lppedd'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    annotationProcessor 'net.bytebuddy:byte-buddy:1.10.8'
    annotationProcessor 'net.bytebuddy:byte-buddy-agent:1.10.8'
    annotationProcessor files('libs/jabel-javac-plugin.jar')
}

intellij {
    version 'IU-2019.2'
    downloadSources = true
    pluginName 'Additional Inspections'
    plugins = ['java']
}

patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

tasks {
    compileJava {
        options.forkOptions.jvmArgs.add '-Djdk.attach.allowAttachSelf=true'
        options.compilerArgs = ['--release', '8', '--enable-preview']

        doFirst {
            options.compilerArgs = options.compilerArgs.findAll {
                it != '--enable-preview'
            }

            println "Current JVM: " + Jvm.current()
        }
    }

    def kotlinCompileOptions = {
        kotlinOptions.languageVersion = '1.4'
        kotlinOptions.jvmTarget = '1.8'
        kotlinOptions.freeCompilerArgs += [
                '-XXLanguage:+NewInference',
                '-Xno-param-assertions',
                '-Xno-call-assertions',
                '-Xopt-in=kotlin.contracts.ExperimentalContracts'
        ]
    }

    compileKotlin kotlinCompileOptions
    compileTestKotlin  kotlinCompileOptions
}
